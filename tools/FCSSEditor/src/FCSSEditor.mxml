<?xml version="1.0" encoding="utf-8"?>
<s:WindowedApplication xmlns:fx="http://ns.adobe.com/mxml/2009" xmlns:s="library://ns.adobe.com/flex/spark" xmlns:mx="library://ns.adobe.com/flex/halo" xmlns:local="*"
					   width="443" height="718" backgroundColor="#F6FEFF"
					   applicationComplete="addListeners()">
	
	<fx:Script>
		<![CDATA[
			import com.flashartofwar.fcss.styles.Style;
			import com.flashartofwar.fcss.stylesheets.FStyleSheet;
			import com.flashartofwar.fcss.utils.CSSTidyUtil;
			
			import flash.events.Event;
			import flash.events.IOErrorEvent;
			import flash.events.MouseEvent;
			import flash.net.FileFilter;
			import flash.net.URLLoader;
			import flash.net.URLRequest;
			
			import mx.containers.TabNavigator;
			import mx.containers.VBox;
			import mx.controls.Alert;
			import mx.controls.textClasses.TextRange;
			
			import net.anirudh.as3syntaxhighlight.CodePrettyPrint;
			import net.anirudh.as3syntaxhighlight.PseudoThread;
			
			import spark.components.NavigatorContent;
			
			private var isURL : Boolean = true;
			private var cssFile : File;
			
			[Bindable]
			private var cssText : String = "";
			[Bindable]
			private var speedText : String = "";
			
			private var styleSheet : FStyleSheet;

			private static const BREAK:String = "--------------------------------------------";
			
			private function addListeners():void
			{
				loadButton.addEventListener(MouseEvent.CLICK, onLoadButtonClick, false, 0 , true);
			}
			
			private function browseForFile():void
			{
				cssFile = new File();
				cssFile.addEventListener(Event.SELECT, onFileSelected, false, 0, true);
				cssFile.browseForOpen("Select CSS File", [new FileFilter("CSS File", ".css")]);
			}
			
			private function setupLoadFromFile(file : File):void
			{
				isURL = false;
				fileInputField.text = file.nativePath;
			}
			
			/**
			 * 
			 * Load a CSS file from the system.
			 * 
			 **/
			private function loadFromFile():void
			{	
				cssFile.addEventListener(Event.COMPLETE, onFileLoaded, false, 0 , true);
				cssFile.load();
				isURL = true;
			}
			
			/**
			 * 
			 * Load CSS file from a url.
			 * 
			 **/
			private function loadFromURL(url : String):void
			{
				if(url == null) Alert.show("Please Enter the URL to the CSS file for select one from your computer.", "Oops No File Specified.");
				var loader:URLLoader = new URLLoader();
				var request:URLRequest = new URLRequest(url);
				
				loader.addEventListener(IOErrorEvent.IO_ERROR, onIOErrorEvent, false, 0, true);
				loader.addEventListener(Event.COMPLETE, onURLLoaded, false, 0, true);
				loader.load(request);
			}
			
			private function populateEditorPane(css : String):void
			{
				cssText = css;
			}
			
			/**
			 * 
			 * Save the file to the file system.
			 * 
			 **/
			private function saveFile():void
			{
				var savedCSSFile : File = new File();
				savedCSSFile.addEventListener(Event.COMPLETE, onFileSaved, false, 0, true);
				
				if(tidyFlag.selected)
				{
					savedCSSFile.save(CSSTidyUtil.tidy(cssText), "editedCSS.css");
				}
				else
				{
					savedCSSFile.save(cssText, "editedCSS.css");
				}
			}
			
			/**
			 * 
			 * Checks if the CSS being loaded is compressed. If so, it will be uncompressed.
			 * 
			 * @param cssData CSS file to be checked.
			 * @return <code>String</code> Uncompressed CSS
			 * 
			 **/
			private function checkCSS(cssData:String):String
			{
				var regEx:RegExp = /(\s)/;
				var css:String;
				
				if(cssData.search(regEx) == 0)
				{
					css = cssData;
				}
				else
				{
					css = CSSTidyUtil.format(cssData);
				}
				
				return css;
			}
			
			/**
			 * 
			 * Fills in the Selector window.
			 * 
			 * @param css CSS that is used to populate the selector window.
			 * 
			 **/
			private function populateSelectors(css:String):void
			{
				var stylesSheet:FStyleSheet = new FStyleSheet();
				var styles:Array;
				var len:Number;
				var i:int = 0;
				
				styleSheet.parseCSS(css);
				styles = styleSheet.styleNames;
				len = styles.length;
				
				for(i ; i < len ; i++)
				{
					trace(styles[i]);
					addField(styles[i]);
				}
			}
			
			/**
			 * 
			 * Adds a new VBox to the Selector Window.
			 * 
			 * @param name Labelfor the VBox.
			 * 
			 **/
			private function addField(name : String):void
			{
				var container:VBox = new VBox();
				
				container.label = name;
				container.width = selectorsField.width;
				container.height = 10;
				
				selectorsField.addChild(container);
			}
			
			
			//---------------------------------------------------------------------
			//-------------------------EventHandlers-------------------------------
			//---------------------------------------------------------------------
			
			protected function onIOErrorEvent(event : IOErrorEvent):void
			{
				Alert.show("URL is not valid.", "Oops URL doesn't exist.");
			}
			
			protected function onFileSelected(event : Event):void
			{
				setupLoadFromFile(event.target as File);
			}
			
			protected function onBrowseClick(event : Event):void
			{
				browseForFile();
			}
			
			protected function onSaveClick(event : Event):void
			{
				saveFile();
			}
			
			protected function onLoadButtonClick(event : Event):void
			{
				(!isURL) ? loadFromFile() : loadFromURL(fileInputField.text);
			}
			
			protected function onFileLoaded(event : Event):void
			{
				var css:String = checkCSS((event.target as File).data.toString());
				fileInputField.text = "";
				startSpeedTest(css);
				populateEditorPane(css);
				populateSelectors(css);
			}
			
			protected function onFileSaved(event : Event):void
			{
				Alert.show("CSS File Saved.", "Save Complete.");
			}
			
			protected function onURLLoaded(event : Event):void
			{
				var css:String = checkCSS((event.target as URLLoader).data.toString());
				fileInputField.text = "";
				startSpeedTest(css);
				populateEditorPane(css);
				populateSelectors(css);
			}
			
			protected function onUpdateText(event : Event):void
			{
				cssText = cssEditorField.text;
				codeHighlight();
				trace("updated");
			}
			
			
			//---------------------------------------------------------------------
			//-------------------------SpeedTest-------------------------------
			//---------------------------------------------------------------------
			
			protected function parseCamoCSS(css : String):void
			{
				//var tidyCSS:String = CSSTidyUtil.tidy(rawCSS);
				styleSheet = new FStyleSheet();	
				speedText = BREAK+"\n* Test FCSS Pre-parser *\n"+BREAK;
				// parse css	
				var t:Number = getTimer();
				
				
				styleSheet.parseCSS(css, true);
				t = (getTimer()-t);
				var strDebug:String;
				strDebug = "\n\tParsing time: " + t + " ms";
				speedText += strDebug += "\n\tBytes: " + css.length + " (" + Math.round(css.length / t) + " bytes/ms)";
			}
			
			private function startSpeedTest(css : String) : void
			{
				parseCamoCSS(css);
				requestAllstyles();
				requestAllstyles();
				
			}
			
			protected function requestAllstyles():void
			{
				speedText += "\n"+BREAK+"\n* Test Requesting Every style *\n"+BREAK;
				
				var t:Number = getTimer();
				var strDebug:String;
				
				var styles:Array = styleSheet.styleNames;
				var total:int = styles.length;
				var i:int;
				var style:Style;
				var styleRequestTime:Number;
				var styleName:String;
				var requestTimes:Number = 0;
				var longestRequest:Number = 0;
				var highestRequest:Number = 0;
				var results:Array = [];
				
				for(i = 0; i < total; i++)
				{
					styleName = styles[i];
					styleRequestTime = getTimer();
					style = styleSheet.getStyle(styleName);
					styleRequestTime = (getTimer()-styleRequestTime);
					if(styleRequestTime > 0)
						results.push({time:styleRequestTime, name:styleName});
					
					requestTimes += styleRequestTime;
					
				}
				t = (getTimer()-t);
				strDebug = "\n\tTotal time to request "+(total-1)+" styles time: " + t + " ms";
				speedText += (strDebug);
				
				speedText += "\n\t"+(styles.length - results.length)+"/"+styles.length+" styles took 0 ms";
				
				var average:Number = results.length/styles.length;
				
				speedText += ("\n\tAverage Style Request Time", average, "ms");
				results.sortOn("time", Array.NUMERIC | Array.DESCENDING); 
				
				var average2:Number = ((requestTimes/total-1) < 0) ? 0 : (requestTimes/total-1);
				
				speedText += ("\n\tShortest Request",results[results.length - 1].time, results[results.length - 1].name," (Styles that take 0 ms do not count.)");
				speedText += ("\n\tLongest Request",results[0].time, results[0].name);
				
				var oneSecondRequests:Number = 0; 
				var longerStyles:String ="";
				for(i = 0; i < results.length; i++)
				{
					if(results[i].time > 1)
						longerStyles += "\n\tStyle '"+results[i].name+"' - "+results[i].time+" ms";
					else
						oneSecondRequests ++;
				}
				
				speedText += ("\n\t"+oneSecondRequests+" styles took 1 ms");
				
				speedText += ("\n\tStyle requests > 1 ms:");
				speedText += (longerStyles);
			}
			
			private function runTest(event : Event):void
			{
				if((event.target as TabNavigator).selectedIndex == 2 && cssText != "")
				{
					startSpeedTest(cssText);
				}
			}
			
			
			
			
			private var cssString:String =".spl {font-family:sandboxcode;color: #4f94cd;} .str { font-family:sandboxcode; color: #880000; } .kwd { font-family:sandboxcode; color: #000088; } .com { font-family:sandboxcode; color: #008800; } .typ { font-family:sandboxcode; color: #0068CF; } .lit { font-family:sandboxcode; color: #006666; } .pun { font-family:sandboxcode; color: #666600; } .pln { font-family:sandboxcode; color: #222222; } .tag { font-family:sandboxcode; color: #000088; } .atn { font-family:sandboxcode; color: #660066; } .atv { font-family:sandboxcode; color: #880000; } .dec { font-family:sandboxcode; color: #660066; } ";
			private var codeStyle:StyleSheet;
			private var codePrettyPrint:CodePrettyPrint;
			private var codeTimer:Timer;
			private var asyncStop:Boolean;
			private var asyncRunning:Boolean;
			private var codeStylePF:StyleSheet;
			private var srclenPF:int;
			private var arrPF:Array;
			private var lenPF:int;
			private var firstNodePF:Boolean;
			private var firstIndexPF:int;
			private var pfasyncrunning:Boolean;
			private var pfasyncstop:Boolean;
			private var desclenPF:int;
			private var colorThread:PseudoThread;
			[Bindable]
			private var asyncCodeState:String;
			
			private function codeHighlight():void
			{
				if ( !codeTimer )
				{
					codeTimer = new Timer(200,1);
					codeTimer.addEventListener(TimerEvent.TIMER, doPrettyPrint);
					
				}	
				
				if ( codeTimer.running )
				{
					codeTimer.stop();
				}
				codeTimer.reset();
				// wait for some time to see if we need to highlight or not
				codeTimer.start();
			}
			
			private function doPrettyPrint(event:TimerEvent=null):void
			{
				if ( !codeStyle )
				{
					codeStyle = new StyleSheet();
					codePrettyPrint = new CodePrettyPrint();
					codeStyle.parseCSS(cssString);
				}
				
				if ( codePrettyPrint.asyncRunning )
				{
					codePrettyPrint.prettyPrintStopAsyc = true;
					callLater(doPrettyPrint);
					return;
				}
				
				if ( pfasyncrunning )
				{
					pfasyncstop = true;
					callLater(doPrettyPrint);
					return;
				}	
				codeHighlightInPlace();
				
			}
			
			private function pfinit(startIdx:int, endIdx:int):void
			{
				codeStylePF = codeStyle;
				srclenPF = endIdx - startIdx;
				arrPF = codePrettyPrint.mainDecorations;
				lenPF = arrPF.length;
				desclenPF = cssText.length;
				firstNodePF = false;
				firstIndexPF = 0;
				pfasyncrunning = false;
				pfasyncstop = false;	
			}
			
			private function processFormattedCodeAsync(startIdx:int, endIdx:int, completeFn:Function, optIdx:int=0):Boolean
			{			
				trace("process");
				if ( pfasyncstop )
				{
					pfasyncrunning = false;
					pfasyncstop = false;
					return false;
				}
				pfasyncrunning = true;
				if ( arrPF == null || srclenPF < 1 ) 
				{
					pfasyncrunning = false;
					return false;
				}
				trace("color worker " + optIdx);
				var tr:TextRange;
				var thecolor:Object;
				var i:int = optIdx;
				if ( i > 0 && i % 5 == 0 )
				{
					asyncCodeState = "Coloring (" + int((i / lenPF) * 100) + "%)...";
				}
				if ( i < lenPF )
				{
					/* find first node */
					if ( arrPF[i] == 0 && firstNodePF == false )
					{        
						firstNodePF = true;					
						return true;
					}
					else if ( arrPF[i] == 0 && firstNodePF == true )
					{
						firstNodePF = false;
						firstIndexPF = i;
						
					} 
					if ( i - 2 > 0 )
					{
						if ( arrPF[i-2]  != arrPF[i] && arrPF[i] < cssText.length )
						{
							tr = new TextRange(cssEditorField, false, arrPF[i-2] + startIdx, arrPF[i] + startIdx);
							thecolor = codeStylePF.getStyle("." + arrPF[i-1]).color;
							tr.color = thecolor;
						}
						
					}
					return true;
					
					
				}
				if ( i > 0 )
				{
					i -= 2;
					if ( arrPF[i] + startIdx < endIdx )
					{
						tr = new TextRange(cssEditorField, false, arrPF[i] + startIdx, endIdx);
						thecolor = codeStylePF.getStyle("." + arrPF[i+1]).color;            
						var totlen:int = cssText.length;
						if ( totlen >= endIdx )
							tr.color = thecolor;
						
					}
				}
				if ( completeFn != null )
					completeFn();
				trace("color worker done");
				pfasyncrunning = false;
				return false;			
				
			}
			
			private function codePFComplete():void
			{
				asyncCodeState = "";
			}
			
			private function codeInPlaceComplete():void
			{	
				asyncCodeState = "Coloring...";
				if ( pfasyncrunning )
				{
					pfasyncstop = true;
					callLater(codeInPlaceComplete);
					return;
				}
				asyncRunning = false;
				
				pfinit(0, cssText.length);
				colorThread = new PseudoThread(this.systemManager, processFormattedCodeAsync, this, [0, cssText.length, codePFComplete, 0], 3, 2);
			}
			
			private function lexInt(idx:int, total:int):void
			{
				if ( idx > 0 && idx % 5 == 0 )
				{
					asyncCodeState = "Lexing (" + int((idx / total) * 100) + "%)...";
				}
			}
			
			private function codeHighlightInPlace():void
			{
				asyncRunning = true;
				asyncCodeState = "Lexing...";
				codePrettyPrint.prettyPrintAsync(cssText, null, codeInPlaceComplete, lexInt, this.systemManager);
				
			}
			
		]]>
	</fx:Script>
	
	<s:layout>
		<s:BasicLayout/>
	</s:layout>
	
	<fx:Style source="FCSSEditor.css"/>
	
	<s:Label text="F*CSS Editor" top="9" left="8"/>
	<s:HGroup height="22" top="33" left="11.5" width="413">
		<s:TextInput id="fileInputField" width="261"/>
		<s:Button label="Browse" click="onBrowseClick(event)"/>
		<s:Button id="loadButton" label="Load"/>
	</s:HGroup>
	<mx:TabNavigator left="9" right="18" top="67" bottom="50" change="runTest(event)">
		<s:NavigatorContent label="Selectors" width="100%" height="100%">
			<mx:Canvas horizontalScrollPolicy="off" verticalScrollPolicy="auto" right="0" left="0" top="0" bottom="0">
				<mx:Accordion id="selectorsField" left="4" right="2" top="3" bottom="6">
				</mx:Accordion>
			</mx:Canvas>
		</s:NavigatorContent>
		<s:NavigatorContent label="CSS Editor" width="100%" height="100%">
			<mx:TextArea id="cssEditorField" text="{cssText}"
						 top="10" bottom="10" right="10" left="10" 
						 verticalScrollPolicy="auto" change="onUpdateText(event)"/>
		</s:NavigatorContent>
		<s:NavigatorContent label="Speed Test" width="100%" height="100%">
			<mx:TextArea id="speedTestField" text="{speedText}"
						 top="10" bottom="10" right="10" left="10" 
						 verticalScrollPolicy="auto"/>
		</s:NavigatorContent>
	</mx:TabNavigator>
	<s:HGroup height="31" width="135" right="18" bottom="10" gap="20">
		<mx:CheckBox id="tidyFlag" label="Tidy"  labelPlacement="left"/>
		<s:Button label="Save" click="onSaveClick(event)"/>
	</s:HGroup>
</s:WindowedApplication>
